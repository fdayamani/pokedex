buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'pl.allegro.tech.build.axion-release' version '1.19.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'pl.allegro.tech.build.axion-release'

    group = 'com.dayamani'

    scmVersion {
        tag {
            prefix = 'pokedex'
            versionSeparator = "-"
        }
        versionIncrementer 'incrementMinor'
        versionCreator { version, position ->
            currentlyOnMaster() ? version : position.revision
        }
        snapshotCreator { version, position ->
            version == position.revision ? "" : "-SNAPSHOT"
        }
        monorepo {
            include(['app', 'ft', 'build.gradle', 'libs.versions.toml'])
        }
    }

    version = scmVersion.version

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }

    tasks.register("printVersion") {
        doLast {
            println "Current version: ${project.version}"
        }
    }
}

def static currentlyOnMaster() {
    def commit = "git rev-parse HEAD".execute().text
    return "git branch -r --contains $commit".execute().text.contains("origin/main")
}
